;用于合成项链幸运
;五条拥有相同额外幸运属性的同一种项链，可以提升其额外幸运属性
;提升之后的属性将体现在第一条项链上，其其它额外属性不会受到影响，另外四条项链将回收
;比如：五条+1幸运的灯笼项链，可以合成一条+2幸运的灯笼项链
[@项链幸运提升]
{
;#IF
;CHECKPKPOINT 2
;#SAY
;我不想和你这样的人打交道.. \ \
;<取消/@exit>
;#ELSEACT
#IF
#ACT
GOTO @提交合成项链

[@提交合成项链]
#IF
#ACT
#SAY
<提升项链幸运属性可是我的绝活，仔细看看以下内容：/SCOLOR=255>\
<1、幸运提升需要五条拥有相同额外幸运值的同一种项链。/SCOLOR=250>\
<2、提升将在第一条项链上进行，该项链的其它属性不会受到影响。/SCOLOR=250>\
<3、提升有一定概率失败，失败后只会返回第一条项链。/SCOLOR=250>\
<4、每次提升需要花费500元宝，每条项链最多额外增加三点幸运。/SCOLOR=250>\ \
<ITEMBOX:0:5:514:25:119:42:42:*:254#幸运项链>
<ITEMBOX:1:5:514:72:119:42:42:*:254#幸运项链>
<ITEMBOX:2:5:514:119:119:42:42:*:254#幸运项链>
<ITEMBOX:3:5:514:166:119:42:42:*:254#幸运项链>
<ITEMBOX:4:5:514:213:119:42:42:*:254#幸运项链>\ 
　　　 　　　         　　　　　          <开始升级/@开始升级>  <我再想想/@exit>\

;[@ItemIntoBox0]
;#IF
;#ACT
;SETUPGRADEITEM 0
;CHANGEITEMADDVALUE -1 1 = 0
;UPDATEBOXITEM 0
;RETURNBOXITEM 0

[@开始升级]
#IF
CHECKBOXITEMCOUNT 0 1
CHECKBOXITEMCOUNT 1 1
CHECKBOXITEMCOUNT 2 1
CHECKBOXITEMCOUNT 3 1
CHECKBOXITEMCOUNT 4 1
#ACT
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
请放入所有用于提升项链幸运的物品！\ \
<返回/@提交合成项链>\ 
<离开/@exit>\ 

;检查物品类型
#IF
COMPARETEXT <$BOXITEM[0].STDMODE> 19
COMPARETEXT <$BOXITEM[1].STDMODE> 19
COMPARETEXT <$BOXITEM[2].STDMODE> 19
COMPARETEXT <$BOXITEM[3].STDMODE> 19
COMPARETEXT <$BOXITEM[4].STDMODE> 19
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
我只能对特定项链进行幸运提升，其它装备爱莫能助！\
<返回/@提交合成项链>\ 
<离开/@exit>\ 

;检测相同物品
#IF
COMPARETEXT <$BOXITEM[1].INDEX> <$BOXITEM[0].INDEX>
COMPARETEXT <$BOXITEM[2].INDEX> <$BOXITEM[0].INDEX>
COMPARETEXT <$BOXITEM[3].INDEX> <$BOXITEM[0].INDEX>
COMPARETEXT <$BOXITEM[4].INDEX> <$BOXITEM[0].INDEX>
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
想要提升【<$BOXITEM[0].NAME>】的【幸运】属性?\
需要提供五条拥有相同额外【幸运】属性的【<$BOXITEM[0].NAME>】！\
你给我的物品好像不全是【<$BOXITEM[0].NAME>】。\ \ 
<返回/@提交合成项链>\ 
<离开/@exit>\ 

;获取额外幸运属性值
#IF
#ACT
SETUPGRADEITEM 0
GETITEMADDVALUE -1 1 <$STR(P0)>
SETUPGRADEITEM 1
GETITEMADDVALUE -1 1 <$STR(P1)>
SETUPGRADEITEM 2
GETITEMADDVALUE -1 1 <$STR(P2)>
SETUPGRADEITEM 3
GETITEMADDVALUE -1 1 <$STR(P3)>
SETUPGRADEITEM 4
GETITEMADDVALUE -1 1 <$STR(P4)>

;检测额外幸运值
#IF
SMALL P0 1
#ACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#SAY
想要提升【<$BOXITEM[0].NAME>】的【幸运】属性?\
需要提供五条拥有相同额外【幸运】属性的【<$BOXITEM[0].NAME>】！\
你给我的【<$BOXITEM[0].NAME>】好像没有额外的【幸运】属性。\ \ 
<重新提交项链/@提交合成项链>\
<离开/@exit>\ 

;检测额外幸运值是否一致
#IF
COMPARETEXT P1 P0
COMPARETEXT P2 P0
COMPARETEXT P3 P0
COMPARETEXT P4 P0
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
想要提升【<$BOXITEM[0].NAME>】的【幸运】属性?\
需要提供五条拥有相同额外【幸运】属性的【<$BOXITEM[0].NAME>】！\
你给我的【<$BOXITEM[0].NAME>】拥有不同的额外【幸运】属性。\ \ 
<重新提交项链/@提交合成项链>\
<离开/@exit>\ 

;重新获取一次额外幸运，并计算提升值
#IF
#ACT
SETUPGRADEITEM 0
GETITEMADDVALUE -1 1 <$STR(P0)>
INC P0 1

;检测最大额外提升点数
#IF
LARGE P0 3
#ACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#SAY
实在抱歉，以我目前的能力，\
【<$BOXITEM[0].NAME>】的【幸运】属性最多提升【3】点。\ \
<离开/@exit>\ 

;检查材料是否充足
#IF
CHECKGAMEGOLD > 499
#ACT
GAMEGOLD - 500
#ELSESAY
什么，你连这点实力都没有，还想我为你服务？\ 
准备好足够的元宝再来找我吧。\ \ 
<离开/@exit>\ 
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK

;失败
#IF
#ACT
MOVR P5 0 1000

#IF
LARGE P5 600
#ACT
RETURNBOXITEM 0
DELBOXITEM 1
DELBOXITEM 2
DELBOXITEM 3
DELBOXITEM 4
BREAK
#SAY
很遗憾，项链幸运提升失败，这也不是我想要的结果。\ \ 
<返回/@提交合成项链>\ 
<离开/@exit>\ 

;幸运合成
#IF
#ACT
SETUPGRADEITEM 0
CHANGEITEMADDVALUE -1 1 = <$STR(P0)>
UPDATEBOXITEM 0
RETURNBOXITEM 0
DELBOXITEM 1
DELBOXITEM 2
DELBOXITEM 3
DELBOXITEM 4
#SAY
你总是如此幸运，【<$BOXITEM[0].NAME>】的【幸运】提升成功！\ \
<返回/@提交合成项链>\ 
<离开/@exit>\ 
}