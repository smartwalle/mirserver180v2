;变量说明
;P1 - 当前已刷新首领怪物次数 
;P2 - 当前已刷新精英怪物次数 
;P3 - 当前已刷新普通怪物次数 
;S$怪物攻城_攻城配置数据 - 从文件加载，怪物攻城攻城配置原始数据
;S$怪物攻城_攻城配置名称 - 分解 S$怪物攻城_攻城配置数据，怪物攻城攻城配置
;S$怪物攻城_首领怪物总次数 - 分解 S$怪物攻城_攻城配置数据，刷新首领怪物次数
;S$怪物攻城_精英怪物总次数 - 分解 S$怪物攻城_攻城配置数据，刷新精英怪物次数
;S$怪物攻城_普通怪物总次数 - 分解 S$怪物攻城_攻城配置数据，刷新普通怪物次数

;S$怪物攻城_地图数据 - 从文件加载，怪物攻城地图原始数据，如：3,盟重省
;S$怪物攻城_地图编号 - 分解 S$怪物攻城_地图数据，地图编号
;S$怪物攻城_地图名称 - 分解 S$怪物攻城_地图数据，地图名称
;S$怪物攻城_刷怪坐标数据 - 从文件加载，刷怪刷新坐标，如：100,100
;S$怪物攻城_刷怪坐标X - 分解 S$怪物攻城_刷怪坐标数据，坐标 X
;S$怪物攻城_刷怪坐标Y - 分解 S$怪物攻城_刷怪坐标数据，坐标 Y
;S$怪物攻城_集中坐标数据 - 从文件加载，刷怪集中坐标，如：100,100
;S$怪物攻城_集中坐标X - 分解 S$怪物攻城_集中坐标数据，坐标 X
;S$怪物攻城_集中坐标Y - 分解 S$怪物攻城_集中坐标数据，坐标 Y
;S$怪物攻城_怪物数据 - 从文件加载，怪物
;S$怪物攻城_怪物名称 - 分解 S$怪物攻城_怪物数据，怪物名称
;S$怪物攻城_怪物数量 - 分解 S$怪物攻城_怪物数据，怪物数量
;S$怪物攻城_刷新范围 - 分解 S$怪物攻城_怪物数据，刷新范围

[@开始怪物攻城]
{
#IF
#ACT
;重置所有本脚本会用到的变量
GOTO @清理变量

;获取攻城攻城配置
#IF
#ACT
GETRANDOMTEXT .\QuestDiary\怪物攻城\攻城配置.txt S$怪物攻城_攻城配置数据
EXTRACTSTRING , <$STR(S$怪物攻城_攻城配置数据)> S$怪物攻城_攻城配置名称 S$怪物攻城_首领怪物总次数 S$怪物攻城_精英怪物总次数 S$怪物攻城_普通怪物总次数

;获取地图
#IF
#ACT
GETRANDOMTEXT .\QuestDiary\怪物攻城\<$STR(S$怪物攻城_攻城配置名称)>\地图.txt S$怪物攻城_地图数据
EXTRACTSTRING , <$STR(S$怪物攻城_地图数据)> S$怪物攻城_地图编号 S$怪物攻城_地图名称

;获取刷怪坐标
GETRANDOMTEXT .\QuestDiary\怪物攻城\<$STR(S$怪物攻城_攻城配置名称)>\坐标\<$STR(S$怪物攻城_地图编号)>.txt S$怪物攻城_刷怪坐标数据
EXTRACTSTRING , <$STR(S$怪物攻城_刷怪坐标数据)> S$怪物攻城_刷怪坐标X S$怪物攻城_刷怪坐标Y

;获取怪物集中点坐标
GETRANDOMTEXT .\QuestDiary\怪物攻城\<$STR(S$怪物攻城_攻城配置名称)>\坐标\<$STR(S$怪物攻城_地图编号)>.txt S$怪物攻城_集中坐标数据
EXTRACTSTRING , <$STR(S$怪物攻城_集中坐标数据)> S$怪物攻城_集中坐标X S$怪物攻城_集中坐标Y

;如果没有获取到地图和坐标信息，则取消攻城
#OR
COMPARETEXT S$怪物攻城_地图数据 
COMPARETEXT S$怪物攻城_刷怪坐标数据
COMPARETEXT S$怪物攻城_集中坐标数据
#ACT
BREAK

;重置已刷怪次数
#IF
#ACT
MOV P1 0
MOV P2 0
MOV P3 0

;首领怪
#IF
#ACT
GOTO @刷新首领怪物

;精英怪
#IF
#ACT
GOTO @刷新精英怪物

;普通怪
#IF
#ACT
GOTO @刷新普通怪物

#IF
#ACT
GOTO @清理变量

[@刷新首领怪物]
#IF
SMALL P1 <$STR(S$怪物攻城_首领怪物总次数)>
#ACT
INC P1 1
GETRANDOMTEXT .\QuestDiary\怪物攻城\<$STR(S$怪物攻城_攻城配置名称)>\首领.txt S$怪物攻城_怪物数据
EXTRACTSTRING , <$STR(S$怪物攻城_怪物数据)> S$怪物攻城_怪物名称 S$怪物攻城_怪物数量 S$怪物攻城_刷新范围
#ELSEACT
BREAK

;没有怪物
#IF
COMPARETEXT S$怪物攻城_怪物数据 
#ACT
BREAK

#IF
#ACT
MONGENEX <$STR(S$怪物攻城_地图编号)> <$STR(S$怪物攻城_集中坐标X)> <$STR(S$怪物攻城_集中坐标Y)> <$STR(S$怪物攻城_怪物名称)> <$STR(S$怪物攻城_刷新范围)> <$STR(S$怪物攻城_怪物数量)> 249
SENDMSG 1 [<$STR(S$怪物攻城_怪物名称)>]率领大量恶魔前往<$STR(S$怪物攻城_地图名称)>(<$STR(S$怪物攻城_集中坐标X)>,<$STR(S$怪物攻城_集中坐标Y)>)，急需各位勇士前往支援!!!
SENDMSG 1 [<$STR(S$怪物攻城_怪物名称)>]率领大量恶魔前往<$STR(S$怪物攻城_地图名称)>(<$STR(S$怪物攻城_集中坐标X)>,<$STR(S$怪物攻城_集中坐标Y)>)，急需各位勇士前往支援!!!
SENDMSG 1 [<$STR(S$怪物攻城_怪物名称)>]率领大量恶魔前往<$STR(S$怪物攻城_地图名称)>(<$STR(S$怪物攻城_集中坐标X)>,<$STR(S$怪物攻城_集中坐标Y)>)，急需各位勇士前往支援!!!
GOTO @刷新首领怪物

[@刷新精英怪物]
#IF
SMALL P2 <$STR(S$怪物攻城_精英怪物总次数)>
#ACT
INC P2 1
GETRANDOMTEXT .\QuestDiary\怪物攻城\<$STR(S$怪物攻城_攻城配置名称)>\精英.txt S$怪物攻城_怪物数据
EXTRACTSTRING , <$STR(S$怪物攻城_怪物数据)> S$怪物攻城_怪物名称 S$怪物攻城_怪物数量 S$怪物攻城_刷新范围
#ELSEACT
BREAK

;没有怪物
#IF
COMPARETEXT S$怪物攻城_怪物数据
#ACT
BREAK

#IF
#ACT
MONGENEX <$STR(S$怪物攻城_地图编号)> <$STR(S$怪物攻城_集中坐标X)> <$STR(S$怪物攻城_集中坐标Y)> <$STR(S$怪物攻城_怪物名称)> <$STR(S$怪物攻城_刷新范围)> <$STR(S$怪物攻城_怪物数量)> 249
GOTO @刷新精英怪物

[@刷新普通怪物]
#IF
SMALL P3 <$STR(S$怪物攻城_普通怪物总次数)>
#ACT
INC P3 1
GETRANDOMTEXT .\QuestDiary\怪物攻城\<$STR(S$怪物攻城_攻城配置名称)>\普通.txt S$怪物攻城_怪物数据
EXTRACTSTRING , <$STR(S$怪物攻城_怪物数据)> S$怪物攻城_怪物名称 S$怪物攻城_怪物数量 S$怪物攻城_刷新范围
#ELSEACT
BREAK

;没有怪物
#IF
COMPARETEXT S$怪物攻城_怪物数据
#ACT
BREAK

#IF
#ACT
MONGENEX <$STR(S$怪物攻城_地图编号)> <$STR(S$怪物攻城_集中坐标X)> <$STR(S$怪物攻城_集中坐标Y)> <$STR(S$怪物攻城_怪物名称)> <$STR(S$怪物攻城_刷新范围)> <$STR(S$怪物攻城_怪物数量)> 246
GOTO @刷新普通怪物

[@清理变量]
#IF
#ACT
MOV P1 0
MOV P2 0
MOV P3 0
MOV S$怪物攻城_攻城配置数据
MOV S$怪物攻城_攻城配置名称
MOV S$怪物攻城_首领怪物总次数
MOV S$怪物攻城_精英怪物总次数
MOV S$怪物攻城_普通怪物总次数
MOV S$怪物攻城_地图数据 
MOV S$怪物攻城_地图编号 
MOV S$怪物攻城_地图名称 
MOV S$怪物攻城_刷怪坐标数据 
MOV S$怪物攻城_刷怪坐标X 
MOV S$怪物攻城_刷怪坐标Y
MOV S$怪物攻城_集中坐标数据
MOV S$怪物攻城_集中坐标X
MOV S$怪物攻城_集中坐标Y
MOV S$怪物攻城_怪物数据
MOV S$怪物攻城_怪物名称
MOV S$怪物攻城_怪物数量
MOV S$怪物攻城_刷新范围
}