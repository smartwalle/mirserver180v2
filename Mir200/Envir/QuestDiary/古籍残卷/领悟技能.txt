;变量说明
;S$领悟技能职业名称 - 职业名称
;S$领悟技能技能名称 - 技能名称
;N$领悟技能古籍残卷数量 - 需要古籍残卷数量
;N$领悟技能元宝数量 - 需要元宝数量
;N$领悟技能等级限制 - 等级条件，比如：需要 35 级
;其它已用变量
;P0 - 元宝条件

[@领悟技能]
{
#IF
CHECKJOB <$STR(S$领悟技能职业名称)>
#ELSEACT
BREAK
#ELSESAY
你还没有足够的能力领悟【<$STR(S$领悟技能技能名称)>】，我也无能为力……\ \
<离开/@exit>\

#IF
CHECKLEVELEX < <$STR(N$领悟技能等级限制)>
#ACT
BREAK
#SAY
你还没有足够的能力领悟【<$STR(S$领悟技能技能名称)>】，我也无能为力……\ \
<离开/@exit>\

#IF
CHECKSKILL  <$STR(S$领悟技能技能名称)>
#ACT
BREAK
#SAY
你已经学习【<$STR(S$领悟技能技能名称)>】技能，不需要重复学习。\ \
<离开/@exit>\

;计算元宝条件
#IF
#ACT
DEC N$领悟技能元宝数量 1
MOV P0 <$STR(N$领悟技能元宝数量)>
INC N$领悟技能元宝数量 1

#IF
CHECKITEM 古籍残卷 <$STR(N$领悟技能古籍残卷数量)>
CHECKGAMEGOLD > <$STR(P0)>
#ACT
TAKE 古籍残卷 <$STR(N$领悟技能古籍残卷数量)>
GAMEGOLD - <$STR(N$领悟技能元宝数量)>
ADDSKILL <$STR(S$领悟技能技能名称)>
#SAY
恭喜你，你已成功领悟【<$STR(S$领悟技能技能名称)>】。\ \
<离开/@exit>\
#ELSESAY
想要领悟技能【<$STR(S$领悟技能技能名称)>】，\
需要<$STR(N$领悟技能古籍残卷数量)>张<古籍残卷>和<$STR(N$领悟技能元宝数量)><元宝>。\
等你准备好了再来找我吧！\ \
<离开/@exit>\
}