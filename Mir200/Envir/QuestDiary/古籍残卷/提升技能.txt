;变量说明
;S$提升技能职业名称 - 职业名称
;S$提升技能技能名称 - 技能名称
;N$提升技能古籍残卷数量 - 需要古籍残卷数量
;N$提升技能元宝数量 - 需要元宝数量
;其它已用变量
;P0 - 元宝条件

[@提升技能]
{
#IF
CHECKJOB <$STR(S$提升技能职业名称)>
#ELSEACT
BREAK
#ELSESAY
我知道的只有这么多了…… \ \
<离开/@exit>\

#IF
CHECKSKILL  <$STR(S$提升技能技能名称)>
#ELSEACT
BREAK
#ELSESAY
你还没有练习【<$STR(S$提升技能技能名称)>】技能，不能提升该技能。\ \
<离开/@exit>\

#IF
CHECKSKILL <$STR(S$提升技能技能名称)> < 3
#ACT
BREAK
#SAY
你的技能【<$STR(S$提升技能技能名称)>】还未达到三级，不能提升该技能。\ \
<离开/@exit>\

#IF
CHECKSKILL <$STR(S$提升技能技能名称)> > 3
#ACT
BREAK
#SAY
你的技能【<$STR(S$提升技能技能名称)>】已经达到四级，不需要提升该技能。\ \
<离开/@exit>\

;计算元宝条件
#IF
#ACT
DEC N$提升技能元宝数量 1
MOV P0 <$STR(N$提升技能元宝数量)>
INC N$提升技能元宝数量 1

#IF
CHECKITEM 古籍残卷 <$STR(N$提升技能古籍残卷数量)>
CHECKGAMEGOLD > <$STR(P0)>
#ACT
TAKE 古籍残卷 <$STR(N$提升技能古籍残卷数量)>
GAMEGOLD - <$STR(N$提升技能元宝数量)>
SKILLLEVEL <$STR(S$提升技能技能名称)> = 4
#SAY
恭喜你，你已成功将技能【<$STR(S$提升技能技能名称)>】提升到四级。\ \
<离开/@exit>\
#ELSESAY
想要提升技能【<$STR(S$提升技能技能名称)>】到四级，\
需要<$STR(N$提升技能古籍残卷数量)>张<古籍残卷>和<$STR(N$提升技能元宝数量)><元宝>。\
等你准备好了再来找我吧！\ \
<离开/@exit>\
}