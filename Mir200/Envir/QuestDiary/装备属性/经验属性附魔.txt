;清除装备原有的经验元素属性，随机计算出新的经验元素属性
;本操作只会影响装备的经验元素属性，对于装备原有的其它元素属性不会有影响
;其它已用变量
;N$附魔属性位置 - 属性位置
;N$附魔元宝数量 - 需要元宝数量
;P0 - 属性值概率计算
;P1 - 元宝真实数量
;P2 - 元宝条件

[@经验属性附魔]
{
;#IF
;CHECKPKPOINT 2
;#SAY
;我不想和你这样的人打交道.. \ \
;<取消/@exit>
;#ELSEACT
#IF
#ACT
GOTO @提交附魔装备

[@提交附魔装备]
#IF
#ACT
RETURNBOXITEM 0

;重置所有本脚本会用到的变量
MOV N$附魔属性位置 7
MOV N$附魔元宝数量 10
#SAY
在我这里，你可以开启装备“增加经验”的<元素属性/@关于元素属性>，该属性会帮助\
你更快累积升级经验，是不是很心动？咱们事先说清楚：\
<1、经验附魔每次收费<$STR(N$附魔元宝数量)>元宝，没有次数限制。/SCOLOR=250>\ 
<2、经验附魔并不一定会成功，原有经验属性可能会消失。/SCOLOR=250>\
<3、经验附魔的结果无法控制，可能会比原有的更差，且不能反悔。/SCOLOR=250>\ \
<ITEMBOX:0:5:514:50:120:42:42:5,6,10,11,15,16,19,20,21,22,23,24,26,30,62,63,64:254#放入要附魔经验属性的装备>\ 
　　　　　        <开始附魔/@开始附魔>    <我再想想/@exit>\

[@关于元素属性]
散落玛法大陆上的每一件装备都是玛法大陆上各位能工巧匠的杰作，\
当初工匠们在打造装备的时候，除了赋予这些装备常见的基础属性，\
还为这些装备注入了一些元素属性。由于这些元素属性过于强大，为\
确保这些属性不为恶魔所用，所以这些属性需要经过特殊的附魔重铸\
之后才能显现并为人所用。由于这项技术在流传的过程中出现一些问\
题，所以现在最多只能挖掘出各项属性{百分之二十/SCOLOR=250}的潜力。\ \
<返回/@提交附魔装备>\
<离开/@exit>\

[@开始附魔]
;检查是否有装备
#IF
CHECKBOXITEMCOUNT 0 1
#ELSESAY
请放入要附魔经验属性的装备！\ \
<返回/@提交附魔装备>\
<离开/@exit>\
#ELSEACT
BREAK

;检查物品类型
#OR
COMPARETEXT <$BOXITEM[0].STDMODE> 5
COMPARETEXT <$BOXITEM[0].STDMODE> 6
COMPARETEXT <$BOXITEM[0].STDMODE> 10
COMPARETEXT <$BOXITEM[0].STDMODE> 11
COMPARETEXT <$BOXITEM[0].STDMODE> 15
COMPARETEXT <$BOXITEM[0].STDMODE> 16
COMPARETEXT <$BOXITEM[0].STDMODE> 19
COMPARETEXT <$BOXITEM[0].STDMODE> 20
COMPARETEXT <$BOXITEM[0].STDMODE> 21
COMPARETEXT <$BOXITEM[0].STDMODE> 22
COMPARETEXT <$BOXITEM[0].STDMODE> 23
COMPARETEXT <$BOXITEM[0].STDMODE> 24
COMPARETEXT <$BOXITEM[0].STDMODE> 26
COMPARETEXT <$BOXITEM[0].STDMODE> 30
COMPARETEXT <$BOXITEM[0].STDMODE> 62
COMPARETEXT <$BOXITEM[0].STDMODE> 63
COMPARETEXT <$BOXITEM[0].STDMODE> 64
#ELSEACT
RETURNBOXITEM 0
BREAK
#ELSESAY
恕我眼拙，你这件宝贝我重来没有见过，不知道该怎么帮助你。\ \ 
<离开/@exit>\

;计算元宝条件
#IF
#ACT
MOV P1 <$STR(N$附魔元宝数量)>
DEC P1 1
MOV P2 <$STR(P1)>
INC P1 1

;检查材料是否充足
#IF
CHECKGAMEGOLD > <$STR(P2)>
#ACT
GAMEGOLD - <$STR(P1)>
#ELSESAY
什么，你连这点实力都没有，还想我为你服务？\ 
准备好足够的元宝再来找我吧。\ \ 
<离开/@exit>\ 
#ELSEACT
RETURNBOXITEM 0
BREAK

#IF
#ACT
SETUPGRADEITEM 0

;清空原有属性
#IF
#ACT
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = 0

;附魔装备属性 
#IF
#ACT
GOTO @更新属性

#IF
#ACT
UPDATEBOXITEM 0
RETURNBOXITEM 0
#SAY
这或许就是最好的结果，我已经尽力了。\ \
<返回/@提交附魔装备>\ 
<离开/@exit>\ 

[@更新属性]
#IF
#ACT
MOVR P0 0 1000

#IF
LARGE P0 950
#ACT
MOVR P0 180 201
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK

#IF
LARGE P0 900
#ACT
MOVR P0 160 181
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK

#IF
LARGE P0 800
#ACT
MOVR P0 140 161
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK

#IF
LARGE P0 700
#ACT
MOVR P0 120 141
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK

#IF
LARGE P0 600
#ACT
MOVR P0 100 121
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK

#IF
LARGE P0 500
#ACT
MOVR P0 80 101
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK

#IF
LARGE P0 400
#ACT
MOVR P0 60 80
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK

#IF
#ACT
MOVR P0 10 61
DIV P0 P0 10
SETNEWITEMVALUE -1 <$STR(N$附魔属性位置)> = <$STR(P0)>
BREAK
}