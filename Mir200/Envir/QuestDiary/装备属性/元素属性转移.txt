;将一件装备的元素属性转移到另一件装备
;转移只能在同类装备间完成，比如项链只能转移到项链
[@元素属性转移]
{
;#IF
;CHECKPKPOINT 2
;#SAY
;我不想和你这样的人打交道.. \ \
;<取消/@exit>
;#ELSEACT
#IF
#ACT
GOTO @提交转移装备

[@提交转移装备]
#IF
#ACT
#SAY
<请再次确认你已经知晓以下规则：/SCOLOR=255>\
<1、要转出的装备必需包含任意元素点属性，且转出后该属性会消失。/SCOLOR=250>\
<2、被转入的装备必需和要转出的装备同类，不同种类装备无法转移。/SCOLOR=250>\
<3、被转入的装备最好不要带有元素点属性，转移元素点会直接替换。/SCOLOR=250>\
<4、每次转移操作需要花费1000元宝。/SCOLOR=250>\ \
<ITEMBOX:0:5:514:50:120:42:42:*:254#转出装备><ITEMBOX:1:5:514:120:120:42:42:*:254#转入装备>\ 
　　　 　　　->　　　　　        <开始转移/@开始转移>    <我再想想/@exit>\

[@ItemIntoBox0]
#IF
#ACT
SETUPGRADEITEM 0

;如果第二个框有物品，则退回该物品
#IF
CHECKBOXITEMCOUNT 1 1
#ACT
RETURNBOXITEM 1

#IF
CHECKNEWITEMVALUE -1 0 > 0
#ACT
BREAK

#IF
CHECKNEWITEMVALUE -1 1 > 0
#ACT
BREAK

#IF
CHECKNEWITEMVALUE -1 2 > 0
#ACT
BREAK

#IF
CHECKNEWITEMVALUE -1 3 > 0
#ACT
BREAK

#IF
CHECKNEWITEMVALUE -1 4 > 0
#ACT
BREAK

#IF
CHECKNEWITEMVALUE -1 5 > 0
#ACT
BREAK

#IF
CHECKNEWITEMVALUE -1 6 > 0
#ACT
BREAK

#IF
CHECKNEWITEMVALUE -1 7 > 0
#ACT
BREAK

#ELSEACT
RETURNBOXITEM 0
MESSAGEBOX 放入的装备没有元素属性，不能进行转换！
BREAK

[@ItemIntoBox1]
#IF
CHECKBOXITEMCOUNT 0 1
#ELSEACT
RETURNBOXITEM 1
MESSAGEBOX 需要先提供转出元素属性的装备，请将准备好的装备放入第一个框内！
BREAK

#OR
COMPARETEXT <$BOXITEM[0].STDMODE> 5
COMPARETEXT <$BOXITEM[0].STDMODE> 6
#ACT
GOTO @武器检测
BREAK

#OR
COMPARETEXT <$BOXITEM[0].STDMODE> 10
COMPARETEXT <$BOXITEM[0].STDMODE> 11
#ACT
GOTO @衣服检测
BREAK

#OR
COMPARETEXT <$BOXITEM[0].STDMODE> 19
COMPARETEXT <$BOXITEM[0].STDMODE> 20
COMPARETEXT <$BOXITEM[0].STDMODE> 21
#ACT
GOTO @项链检测
BREAK

#OR
COMPARETEXT <$BOXITEM[0].STDMODE> 22
COMPARETEXT <$BOXITEM[0].STDMODE> 23
#ACT
GOTO @戒指检测
BREAK

#OR
COMPARETEXT <$BOXITEM[0].STDMODE> 24
COMPARETEXT <$BOXITEM[0].STDMODE> 26
#ACT
GOTO @手镯检测
BREAK

#IF
COMPARETEXT <$BOXITEM[0].STDMODE> <$BOXITEM[1].STDMODE>
#ELSEACT
RETURNBOXITEM 1
MESSAGEBOX 不同种类的装备不能进行元素属性转移！
BREAK

[@武器检测]
#OR
COMPARETEXT <$BOXITEM[1].STDMODE> 5
COMPARETEXT <$BOXITEM[1].STDMODE> 6
#ELSEACT
RETURNBOXITEM 1
MESSAGEBOX 不同种类的装备不能进行元素属性转移！
BREAK

[@衣服检测]
#OR
COMPARETEXT <$BOXITEM[1].STDMODE> 10
COMPARETEXT <$BOXITEM[1].STDMODE> 11
#ELSEACT
RETURNBOXITEM 1
MESSAGEBOX 不同种类的装备不能进行元素属性转移！
BREAK

[@项链检测]
#OR
COMPARETEXT <$BOXITEM[1].STDMODE> 19
COMPARETEXT <$BOXITEM[1].STDMODE> 20
COMPARETEXT <$BOXITEM[1].STDMODE> 21
#ELSEACT
RETURNBOXITEM 1
MESSAGEBOX 不同种类的装备不能进行元素属性转移！
BREAK

[@戒指检测]
#OR
COMPARETEXT <$BOXITEM[1].STDMODE> 22
COMPARETEXT <$BOXITEM[1].STDMODE> 23
#ELSEACT
RETURNBOXITEM 1
MESSAGEBOX 不同种类的装备不能进行元素属性转移！
BREAK

[@手镯检测]
#OR
COMPARETEXT <$BOXITEM[1].STDMODE> 24
COMPARETEXT <$BOXITEM[1].STDMODE> 26
#ELSEACT
RETURNBOXITEM 1
MESSAGEBOX 不同种类的装备不能进行元素属性转移！
BREAK

[@开始转移]
#IF
CHECKBOXITEMCOUNT 0 1
CHECKBOXITEMCOUNT 1 1
#ACT
#ELSESAY
请放入要进行元素属性转移的装备！\ \
<返回/@提交转移装备>\ 
<离开/@exit>\ 
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
BREAK

;检查材料是否充足
#IF
CHECKGAMEGOLD > 999
#ACT
GAMEGOLD - 1000
#ELSESAY
什么，你连这点实力都没有，还想我为你服务？\ 
准备好足够的元宝再来找我吧。\ \ 
<离开/@exit>\ 
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
BREAK

#IF
#ACT
SETUPGRADEITEM 1
SETNEWITEMVALUE -1 0 = <$BOXITEM[0].VALUE1>
SETNEWITEMVALUE -1 1 = <$BOXITEM[0].VALUE2>
SETNEWITEMVALUE -1 2 = <$BOXITEM[0].VALUE3>
SETNEWITEMVALUE -1 3 = <$BOXITEM[0].VALUE4>
SETNEWITEMVALUE -1 4 = <$BOXITEM[0].VALUE5>
SETNEWITEMVALUE -1 5 = <$BOXITEM[0].VALUE6>
SETNEWITEMVALUE -1 6 = <$BOXITEM[0].VALUE7>
SETNEWITEMVALUE -1 7 = <$BOXITEM[0].VALUE8>

SETUPGRADEITEM 0
SETNEWITEMVALUE -1 0 = 0
SETNEWITEMVALUE -1 1 = 0
SETNEWITEMVALUE -1 2 = 0
SETNEWITEMVALUE -1 3 = 0
SETNEWITEMVALUE -1 4 = 0
SETNEWITEMVALUE -1 5 = 0
SETNEWITEMVALUE -1 6 = 0
SETNEWITEMVALUE -1 7 = 0

UPDATEBOXITEM 0
RETURNBOXITEM 0
UPDATEBOXITEM 1
RETURNBOXITEM 1
#SAY
一切都在我的掌控之内，元素属性转移成功！\ \
<返回/@提交转移装备>\ 
<离开/@exit>\ 
}