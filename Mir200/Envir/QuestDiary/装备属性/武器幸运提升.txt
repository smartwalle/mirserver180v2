;用于合成武器幸运
;五把拥有相同额外幸运属性的同一种武器，可以提升其额外幸运属性
;提升之后的属性将体现在第一把武器上，其其它额外属性不会受到影响，另外四把武器将回收
;比如：五把+1幸运的裁决之杖，可以合成一把+2幸运的裁决之杖
[@武器幸运提升]
{
;#IF
;CHECKPKPOINT 2
;#SAY
;我不想和你这样的人打交道.. \ \
;<取消/@exit>
;#ELSEACT
#IF
#ACT
GOTO @提交合成武器

[@提交合成武器]
#IF
#ACT
#SAY
<提升武器幸运属性可是我的绝活，仔细看看以下内容：/SCOLOR=255>\
<1、幸运提升需要五把拥有相同额外幸运值的同一种武器。/SCOLOR=250>\
<2、提升将在第一把武器上进行，该武器的其它属性不会受到影响。/SCOLOR=250>\
<3、提升有一定概率失败，失败后只会返回第一把武器。/SCOLOR=250>\
<4、每次提升需要花费500元宝，每把武器最多额外增加七点幸运。/SCOLOR=250>\ \
<ITEMBOX:0:5:514:25:119:42:42:*:254#幸运武器>
<ITEMBOX:1:5:514:72:119:42:42:*:254#幸运武器>
<ITEMBOX:2:5:514:119:119:42:42:*:254#幸运武器>
<ITEMBOX:3:5:514:166:119:42:42:*:254#幸运武器>
<ITEMBOX:4:5:514:213:119:42:42:*:254#幸运武器>\ 
　　　 　　　         　　　　　          <开始升级/@开始升级>  <我再想想/@exit>\

;[@ItemIntoBox0]
;#IF
;#ACT
;SETUPGRADEITEM 0
;CHANGEITEMADDVALUE -1 3 = 0
;UPDATEBOXITEM 0
;RETURNBOXITEM 0

[@开始升级]
#IF
CHECKBOXITEMCOUNT 0 1
CHECKBOXITEMCOUNT 1 1
CHECKBOXITEMCOUNT 2 1
CHECKBOXITEMCOUNT 3 1
CHECKBOXITEMCOUNT 4 1
#ACT
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
请放入所有用于提升武器幸运的物品！\ \
<返回/@提交合成武器>\ 
<离开/@exit>\ 

;检查物品类型
#OR
COMPARETEXT <$BOXITEM[0].STDMODE> 5
COMPARETEXT <$BOXITEM[0].STDMODE> 6
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
我只能对武器进行幸运提升，其它装备爱莫能助！\
<返回/@提交合成武器>\ 
<离开/@exit>\ 

#OR
COMPARETEXT <$BOXITEM[1].STDMODE> 5
COMPARETEXT <$BOXITEM[1].STDMODE> 6
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
我只能对武器进行幸运提升，其它装备爱莫能助！\ \
<返回/@提交合成武器>\ 
<离开/@exit>\ 

#OR
COMPARETEXT <$BOXITEM[2].STDMODE> 5
COMPARETEXT <$BOXITEM[2].STDMODE> 6
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
我只能对武器进行幸运提升，其它装备爱莫能助！\ \
<返回/@提交合成武器>\ 
<离开/@exit>\ 

#OR
COMPARETEXT <$BOXITEM[3].STDMODE> 5
COMPARETEXT <$BOXITEM[3].STDMODE> 6
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
我只能对武器进行幸运提升，其它装备爱莫能助！\ \
<返回/@提交合成武器>\ 
<离开/@exit>\ 

#OR
COMPARETEXT <$BOXITEM[4].STDMODE> 5
COMPARETEXT <$BOXITEM[4].STDMODE> 6
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
我只能对武器进行幸运提升，其它装备爱莫能助！\ \
<返回/@提交合成武器>\ 
<离开/@exit>\ 

;检测相同物品
#IF
COMPARETEXT <$BOXITEM[1].INDEX> <$BOXITEM[0].INDEX>
COMPARETEXT <$BOXITEM[2].INDEX> <$BOXITEM[0].INDEX>
COMPARETEXT <$BOXITEM[3].INDEX> <$BOXITEM[0].INDEX>
COMPARETEXT <$BOXITEM[4].INDEX> <$BOXITEM[0].INDEX>
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
想要提升【<$BOXITEM[0].NAME>】的【幸运】属性?\
需要提供五把拥有相同额外【幸运】属性的【<$BOXITEM[0].NAME>】！\
你给我的物品好像不全是【<$BOXITEM[0].NAME>】。\ \ 
<返回/@提交合成武器>\ 
<离开/@exit>\ 

;获取额外幸运属性值
#IF
#ACT
SETUPGRADEITEM 0
GETITEMADDVALUE -1 3 <$STR(P0)>
SETUPGRADEITEM 1
GETITEMADDVALUE -1 3 <$STR(P1)>
SETUPGRADEITEM 2
GETITEMADDVALUE -1 3 <$STR(P2)>
SETUPGRADEITEM 3
GETITEMADDVALUE -1 3 <$STR(P3)>
SETUPGRADEITEM 4
GETITEMADDVALUE -1 3 <$STR(P4)>

;检测是否有额外幸运值
#IF
SMALL P0 1
#ACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#SAY
想要提升【<$BOXITEM[0].NAME>】的【幸运】属性?\
需要提供五把拥有相同额外【幸运】属性的【<$BOXITEM[0].NAME>】！\
你给我的【<$BOXITEM[0].NAME>】好像没有额外的【幸运】属性。\ \ 
<重新提交武器/@提交合成武器>\
<离开/@exit>\ 

;检测额外幸运值是否一致
#IF
COMPARETEXT P1 P0
COMPARETEXT P2 P0
COMPARETEXT P3 P0
COMPARETEXT P4 P0
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#ELSESAY
想要提升【<$BOXITEM[0].NAME>】的【幸运】属性?\
需要提供五把拥有相同额外【幸运】属性的【<$BOXITEM[0].NAME>】！\
你给我的【<$BOXITEM[0].NAME>】拥有不同的额外【幸运】属性。\ \ 
<重新提交武器/@提交合成装备>\
<离开/@exit>\ 

;重新获取一次额外幸运，并计算提升值
#IF
#ACT
SETUPGRADEITEM 0
GETITEMADDVALUE -1 3 <$STR(P0)>
INC P0 1

;检测最大额外提升点数
#IF
LARGE P0 7
#ACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK
#SAY
实在抱歉，以我目前的能力，\
【<$BOXITEM[0].NAME>】的【幸运】属性最多提升【7】点。\ \
<离开/@exit>\ 

;检查材料是否充足
#IF
CHECKGAMEGOLD > 499
#ACT
GAMEGOLD - 500
#ELSESAY
什么，你连这点实力都没有，还想我为你服务？\ 
准备好足够的元宝再来找我吧。\ \ 
<离开/@exit>\ 
#ELSEACT
RETURNBOXITEM 0
RETURNBOXITEM 1
RETURNBOXITEM 2
RETURNBOXITEM 3
RETURNBOXITEM 4
BREAK

;失败
#IF
#ACT
MOVR P5 0 1000

#IF
LARGE P5 600
#ACT
RETURNBOXITEM 0
DELBOXITEM 1
DELBOXITEM 2
DELBOXITEM 3
DELBOXITEM 4
BREAK
#SAY
很遗憾，武器幸运提升失败，这也不是我想要的结果。\ \ 
<返回/@提交合成武器>\ 
<离开/@exit>\ 

;幸运合成
#IF
#ACT
SETUPGRADEITEM 0
CHANGEITEMADDVALUE -1 3 = <$STR(P0)>
UPDATEBOXITEM 0
RETURNBOXITEM 0
DELBOXITEM 1
DELBOXITEM 2
DELBOXITEM 3
DELBOXITEM 4
#SAY
你总是如此幸运，【<$BOXITEM[0].NAME>】的【幸运】提升成功！\ \
<返回/@提交合成武器>\ 
<离开/@exit>\ 
}